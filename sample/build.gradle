apply plugin: 'com.android.application'

def getVariableOrEmpty(String name) {
  if (project.hasProperty(name)) {
    return project.property(name)
  } else {
    return ""
  }
}

android {
  compileSdkVersion setup.compileSdk
  buildToolsVersion setup.buildTools

  defaultConfig {
    applicationId "debug_artist.sample"

    minSdkVersion setup.minSdk
    targetSdkVersion setup.targetSdk

    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    debug {
      minifyEnabled true
      shrinkResources true

      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'rules.pro'

      buildConfigField "String", "PIVOTAL_API_KEY", "\"${getVariableOrEmpty("DA_PIVOTAL_API_KEY")}\""
      buildConfigField "String", "PIVOTAL_PROJECT_ID", "\"${getVariableOrEmpty("DA_PIVOTAL_PROJECT_ID")}\""
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'rules.pro'
    }
  }

  signingConfigs {
    // Added to be able to assemble release apks with shrink
    debug {
      keyAlias "sample"
      keyPassword "sample"
      storePassword "sample"
      storeFile file("../sample.jks")
    }
    release {
      keyAlias "sample"
      keyPassword "sample"
      storePassword "sample"
      storeFile file("../sample.jks")
    }
  }

  lintOptions {
    abortOnError false
  }

  packagingOptions {
    exclude 'META-INF/lib_release.kotlin_module'
  }
}

def sampleVersions = [retrofit: "2.2.0",
                      okhttp  : "3.8.0"]

dependencies {
  compile libraries.appCompat
  compile project(':lib')
  compile project(':reporter-pivotal')

  // For StethoActivity
  compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
  compile "com.squareup.retrofit2:retrofit:${sampleVersions.retrofit}"
  compile "com.squareup.retrofit2:converter-gson:${sampleVersions.retrofit}"
  compile "com.squareup.retrofit2:converter-gson:${sampleVersions.retrofit}"
  compile "com.squareup.okhttp3:okhttp:${sampleVersions.okhttp}"
  compile "com.squareup.okhttp3:logging-interceptor:${sampleVersions.okhttp}"
}